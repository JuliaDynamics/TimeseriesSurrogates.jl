export IAAFT
"""
    IAAFT(M = 100, tol = 1e-6, W = 75)

An iteratively adjusted amplitude-adjusted-fourier-transform surrogate[^SchreiberSchmitz1996].

IAAFT surrogate have the same linear correlation, or periodogram, and also
preserves the amplitude distribution of the original data, but are improved relative
to AAFT through iterative adjustment (which runs for a maximum of `M` steps).
During the iterative adjustment, the periodograms of the original signal and the
surrogate are coarse-grained and the powers are averaged over `W` equal-width
frequency bins. The iteration procedure ends when the relative deviation
between the periodograms is less than `tol` (or when `M` is reached).

IAAFT, just as AAFT, can be used to test the null hypothesis that the data 
come from a monotonic nonlinear transformation of a linear Gaussian process.

[^SchreiberSchmitz1996]: T. Schreiber; A. Schmitz (1996). "Improved Surrogate Data for Nonlinearity Tests". [Phys. Rev. Lett. 77 (4)](https://journals.aps.org/prl/abstract/10.1103/PhysRevLett.77.635)
"""
struct IAAFT <: Surrogate
    M::Int
    tol::Real
    W::Int

    function IAAFT(;M::Int = 100, tol::Real = 1e-6, W::Int = 75)
        new(M, tol, W)
    end
end

Base.show(io::IO, x::IAAFT) = print(io, "IAAFT(M=$(x.M), tol=$(x.tol), W=$(x.W))")

function surrogenerator(x, method::IAAFT, rng = Random.default_rng())
    # Pre-plan Fourier transforms
    forward = plan_rfft(x)
    inverse = plan_irfft(forward*x, length(x))

    # Pre-compute stuff that can be used for different surrogate realizations
    m = mean(x)
    x_sorted = sort(x)
    ð“• = forward*(x .- m)
    r_original = abs.(ð“•)

    # Coarse-grain the periodograms when comparing them between iterations.
    px = DSP.periodogram(x)
    range = LinRange(0.0, 0.5, method.W)
    px_binned = interp(px.freq, px.power, range)

    # These are updated during iteration procedure
    ð“•new = Vector{Complex{Float64}}(undef, length(ð“•))
    ð“•sorted = Vector{Complex{Float64}}(undef, length(ð“•))
    Ï•sorted = Vector{Complex{Float64}}(undef, length(ð“•))

    init = (forward = forward, inverse = inverse, m = m, ð“• = ð“•, r_original = r_original,
            px_binned = px_binned, range = range, x_sorted = x_sorted,
            ð“•new = ð“•new, ð“•sorted =  ð“•sorted, Ï•sorted = Ï•sorted)

    return SurrogateGenerator(method, x, init, rng)
end

function (sg::SurrogateGenerator{<:IAAFT})()
    init_fields = (:forward, :inverse, :m, :ð“•, :r_original,
                    :px_binned, :range,
                    :x_sorted,
                    :ð“•new, :ð“•sorted, :Ï•sorted)
    forward, inverse, m, ð“•, r_original,
        px_binned, range,
        x_sorted,
        ð“•new, ð“•sorted, Ï•sorted = getfield.(Ref(sg.init), init_fields)

    x = sg.x
    M = sg.method.M
    tol = sg.method.tol

    # Keep track of difference between periodograms between iterations
    diffs = zeros(Float64, 2)

    # RANK ORDERING.
    # Create some Gaussian noise, and find the indices that sorts it with
    # increasing amplitude. Then sort the original time series according to
    # the indices rendering the Gaussian noise sorted.
    n = length(x)
    g = rand(sg.rng, Normal(), n)
    ts_sorted = x[sortperm(g)]

    # The surrogate
    s = Vector{Float64}(undef, n)

    iter = 1
    success = false
    while iter <= M
        # Take the Fourier transform of `ts_sorted` and get the phase angles of
        # the resulting complex numbers.
        ð“•sorted .= forward * ts_sorted
        Ï•sorted .= angle.(ð“•sorted)

        # The new spectrum preserves the amplitudes of the Fourier transform of
        # the original time series, but randomises the phases (because the
        # phases are derived from the *randomly sorted* version of the original
        # time series).
        ð“•new .= r_original .* exp.(Ï•sorted .* 1im)

        # Now, let the surrogate time series be the values of the original time
        # series, but sorted according to the new spectrum. The shuffled series
        # is generated by taking the inverse Fourier transform of the spectrum
        # consisting of the original amplitudes, but having randomised phases.
        s .= real.(inverse * ð“•new) # ifft normalises by default

        # map original values onto shuffle
        s[sortperm(s)] = x_sorted
        ts_sorted .= s

        # Convergence check on periodogram
        ps = DSP.periodogram(s)
        ps_binned = interp(ps.freq, ps.power, range)

        if iter == 1
            diffs[1] = sum((px_binned[2] .- ps_binned[2]).^2) / sum(px_binned[2].^2)
        else
            diffs[2] = sum((px_binned[2] .- ps_binned[2]).^2) / sum(px_binned[2].^2)
            abs(diffs[1] - diffs[2]) < tol ? break : diffs[1] = copy(diffs[2])
        end

        iter += 1
    end

    return s
end


include("../utils/powerspectrum.jl")
include("../utils/interpolation.jl")
export IAAFT2

struct IAAFT2 <: Surrogate
    M::Int
    tol::Real
    W::Int

    function IAAFT2(;M::Int = 100, tol::Real = 1e-6, W::Int = 75)
        new(M, tol, W)
    end
end

Base.show(io::IO, x::IAAFT2) = print(io, "IAAFT2(M = $(x.M), tol = $(x.tol), W = $(x.W))")


function interpolated_spectrum(spectrum, n)
    intp_spectrum = zeros(n)
    interpolated_spectrum!(intp_spectrum, spectrum, n)
end

function interpolated_spectrum!(intp_spectrum, spectrum, n)
    t = 1:length(spectrum)
    r = getrange(t, n)
    iâ‚“ = itp(spectrum)
    intp_spectrum .= iâ‚“.(r)
    return intp_spectrum
end

function surrogenerator(x, method::IAAFT2, rng = Random.default_rng())
    m = mean(x)

    # Pre-plan Fourier transforms
    forward = plan_rfft(x)
    inverse = plan_irfft(forward * x, length(x))

    # Initial forward transform.
    ð“• = forward * x

    # For periodogram
    ð“•p = prepare_spectrum(x, forward)

    # Amplitudes of the initial transform are kept 
    # constant during iterations, so pre-allocate.
    r = abs.(ð“•)

    # Pre-allocate angles (these change during iteration)
    Ï• = abs.(ð“•)

    # Sorted values of the original time series are used for the rescaling step.
    xÌ‚ = sort(x)

    # Initial power spectrum
    #xpower = DSP.periodogram(x).power

    # We'll re-use the plan both for periodograms and for Fourier transforms 
    # during iterations.
    xpower = zeros(length(ð“•)); powerspectrum!(ð“•p, xpower, x, forward)
    spower = copy(xpower)

    # Binned periodograms
    xpoweráµ¦ = interpolated_spectrum(xpower, method.W)
    spoweráµ¦ = interpolated_spectrum(spower, method.W)

    init = (
        forward = forward, 
        inverse = inverse, 
        ð“• = ð“•, 
        ð“•p = ð“•p,
        r = r, 
        Ï• = Ï•, 
        m = m, 
        xÌ‚ = xÌ‚, 
        xpower = xpower, 
        spower = spower,
        xpoweráµ¦ = xpoweráµ¦,
        spoweráµ¦ = spoweráµ¦,
    )

    return SurrogateGenerator2(method, x, similar(x), init, rng)
end
using LinearAlgebra
function (sg::SurrogateGenerator2{<:IAAFT2})()
    init_fields = (:forward, :inverse, :ð“•, :ð“•p, :r, :Ï•, :m, :xÌ‚, :xpower, :spower, :xpoweráµ¦, :spoweráµ¦)
    forward, inverse, ð“•, ð“•p, r, Ï•, m, xÌ‚, xpower, spower, xpoweráµ¦, spoweráµ¦ = getfield.(Ref(sg.init), init_fields)

    x, s, rng = sg.x, sg.s, sg.rng
    M, W = sg.method.M, sg.method.W
    tol = sg.method.tol

    # Surrogate starts out as a random permutation of `x`
    n = length(x)
    s .= x[sample(rng, 1:n, n)]

    # Index vector used to sort in-place
    ix = zeros(Int, length(x))

    # Keep track of difference between periodograms between iterations
    sum_old, sum_new = 0.0, 0.0

    iter = 1
    while iter <= M        
        # Fourier transform of the surrogate. The forward transformation can be done 
        # in-place to avoid allocations.
        mul!(ð“•, forward, s) # ð“• .= forward * s
        
        # Replace amplitudes of the transform with the original amplitudes `r`,
        # leavding phases untouched.
        Ï• .= angle.(ð“•)
        ð“• .= r .* exp.(Ï• .* 1im)

        # TODO: Unfortunately, we can't simply do ldiv! here to avoid allocations. 
        # But, although FFTW does not yet have irfft!, it will probably have. 
        # See https://github.com/JuliaMath/FFTW.jl/pull/222. 
        # Once that PR is merged, we should replace the following line with 
        # the in-place version.
        ######################################################################
        # The surrogate is initially the real part of the inverse transform.
        s .= inverse * ð“• 

        # The inverse transform does not preserve the original values of the time series, 
        # because the phases are randomized. We therefore rank-order `s` (sort it), 
        # and replace its value with the sorted `x` values.
        sortperm!(ix, s)
        s[ix] .= xÌ‚

        # Compute and interpolate spectrum for surrogate
        powerspectrum!(ð“•p, spower, s, forward)
        interpolated_spectrum!(spoweráµ¦, spower, W)

        # Compute power spectrum for the current state of the surrogate `s` and 
        # the original power spectrum.
        if iter == 1
            sum_old = sum((xpoweráµ¦ .- xpoweráµ¦) .^ 2) / sum(xpoweráµ¦ .^ 2)
        else 
            sum_new = sum((xpoweráµ¦ .- spoweráµ¦) .^ 2) / sum(xpoweráµ¦ .^ 2)
            if abs(sum_old - sum_new) < tol
                iter = M + 1
            else
                sum_old = sum_new
            end
        end

        iter += 1
    end

    return s
end
